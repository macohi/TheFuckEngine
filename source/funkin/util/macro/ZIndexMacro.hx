package funkin.util.macro;

#if (macro && !display)
import haxe.macro.Context;
import haxe.macro.Expr;

/**
 * A macro class for implementing z-ordering features.
 */
class ZIndexMacro
{
    public static macro function buildFlxBasic():Array<Field>
    {
        var fields = Context.getBuildFields();
        var has:Bool = false;

        for (field in fields)
        {
            if (field.name != 'zIndex') continue;
            has = true;
        }

        if (!has)
        {
            fields.push({
                name: 'zIndex',
                access: [APublic],
                kind: FieldType.FVar(macro:Int, macro $v{0}),
                pos: Context.currentPos()
            });
        }

        return fields;
    }

    public static macro function buildFlxGroup():Array<Field>
    {
        var fields = Context.getBuildFields();
        var has:Bool = false;

        for (field in fields)
        {
            if (field.name != 'refresh') continue;
            has = true;
        }

        if (!has)
        {
            fields.push({
                name: 'refresh',
                access: [APublic],
                kind: FieldType.FFun({
                    args: [],
                    expr: macro { sort((i, basic1, basic2) -> return flixel.util.FlxSort.byValues(FlxSort.ASCENDING, basic1.zIndex, basic2.zIndex)); }
                }),
                pos: Context.currentPos()
            });
        }

        return fields;
    }
}
#end